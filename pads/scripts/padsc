#!/bin/tcsh

# file paths will be filled in by install script

# padsc : front-end script to padsc compiler
# 
# features of this front-end script:
#         1. If all the .p files are in the same dir P 
#            and there is a 'gen' dir at the same level
#            as P, the generated files are placed in gen.
#            This auto retargeting does not occur if
#            there is no gen, or an explicit -p option
#            is already present, or if some .p files
#            are in different locations.
#
#         2. The script needs to determine $PADS_HOME, the location
#            of the PADS installation, so that it can find
#            $PADS_HOME/include and $PADS_HOME/lib.
#            If environment variable PADS_HOME is set, then
#            this is used, otherwise the script guess the
#            location based on argument 0, e.g., if you invoke
#            the script using path ../foo/padsc, then it
#            sets PADS_HOME to ../foo 


set firstout = "\n"

if (!($?PADS_HOME)) then
  if ($0:h == $0:t) then
    set PADS_HOME = .
  else
    set PADS_HOME = $0:h
  endif
  echo $firstout"env variable PADS_HOME not set, guessing value $PADS_HOME based on invocation path\n"
  set firstout = ""
endif

if (!(-d $PADS_HOME && -d $PADS_HOME/lib && -d $PADS_HOME/include && -e $PADS_HOME/padsc)) then
  echo $firstout"Directory $PADS_HOME does not appear to be a valid PADS installation dir."
  echo "Set env variable PADS_HOME to the correct location and try again"
  exit 0
endif

set arch = `$PADS_HOME/scripts/package`

set IMAGE = $PADS_HOME/lib/padsc
set SMLBIN = $PADS_HOME/binlinks/smlruntime.$arch

if ($arch == linux.i386) then 
    set lua = -U__GNUC__
    set lda = -D__LINUX_PREPROCESSOR_FIXES
else 
    set lua =
    set lda = 
endif

set opts = ""
set pfiles = ""
set retarg = ""
set retarg_OK = 1
set help = 0

#set echo
foreach x ($*)
    switch ($x)
    case -r:
      set retarg_OK = 0
      breaksw
    case -help:
    case --help:
      set help = 1
    case -*:
      set opts = "$opts $x"
      breaksw
    default:
      set tltmp = $x:t
      set y = `basename $tltmp .p`
      if ($tltmp == $y) then
        set opts = "$opts $x"
      else
        set pfiles = "$pfiles $x"
        set zh = $x:h
        set zt = $x:t
        if ($zh == $zt) then
          set pdir = "."
          set tdir = "../gen"
        else
          set pdir = $zh
          set zhh = $zh:h
          set zht = $zh:t
          if ($zhh == $zht) then
            set tdir = "./gen"
          else
            set tdir = $zhh/gen
          endif
	  if (!(-d $tdir)) then
	    set tdir = $pdir/../gen
	  endif
        endif
        if (-d $pdir && -d $tdir) then
          if ($retarg == "") then
            set retarg = $tdir
            #echo "initial retarg $retarg"
          else
            #echo "checking another tdir against retarg"
            if ($tdir != $retarg) then
              #echo "tdir $tdir != retarg $retarg"
              set retarg_OK = 0
            endif
          endif
        else
          #echo "pdir $pdir or tdir $tdir are not dirs"
          set retarg_OK = 0
        endif
      endif
      breaksw
    endsw
end # forech

#echo "full_args = $*"
#echo "pfiles    = $pfiles"
#echo "opts      = $opts"
#echo "retarg_OK = $retarg_OK"
#echo "retarg    = $retarg"

if ($help) then
  set cmd = "-help"
else
  if ("$pfiles" == "") then
    echo $firstout"No .p files specified.  Usage:  $0 <options> <.p files>\n\tUse --help for all available options.\n"
    exit -1
  endif
  if ($retarg_OK) then
      set cmd = "-r $retarg $opts $pfiles"
      echo $firstout"Chose target dir $retarg\n"
      set firstout = ""
  else
      set cmd = "$*"
  endif
endif

#echo $firstout"cmd = $cmd\n"
#set firstout = ""
#exit 0

exec $SMLBIN @SMLload=$IMAGE $PADS_HOME $cmd -I $PADS_HOME/include -I /home/gsf/arch/$arch/include/ast \
$lua $lda < /dev/null

